


		;********************************************************
		;*          THIS MODULE WILL BE CALLED BY AN INTERUPT   *
		;*     EVERY 10mS.  IT CHECKS THE STATE OF THE          *
		;*     MASTER CONTROL RELAY TO SEE IF TO SEE IF         *
		;*     THE MACHINE IS ENABLED.  SEVERAL OF THE          *
		;*     CONTROL SWITCHES AND POSITION SENSORS ARE        *
		;*     CHECKED TO UPDATE THE MACHINE RUNNING MODE.      *
		;*     A ROUTINE IS CALLED TO CHECK TIMERS THAT         *
		;*     CONTROL THE ON TIME OF THE TEST, KICKOUT,        *
		;*     AND LOW PARTS OUTPUTS.				*
		;********************************************************

	NAME	TIMERS

	ORG	0800H

	EXTRN	CHREC,ERRREG,REG1,REG2,CALREG,TIMEREG,OUTCNT,RUNREG
	EXTRN	BOWLCNT,ESICNT,STPREG,MCRCNT,PORT06,BOWLDWN,ESIDWN
	EXTRN	OUTDWN,PORT04,AIRDWN,MCRDWN,SHIFT2,SLOWDWN,SLOWCNT
	EXTRN	OVENDWN,OVENTME,AIRCNT,ESIRLY,DISPOLL,BEEPCNT

	GLOBAL	UPDATE

			;*************************************************
			;*   THE FIRST PART OF THIS MODULE STORES THE	*
			;* REGISTERS ON THE STACK AND CALLS THE TT5 	*
			;* ROUTINE TO SEE IF THERE HAS BEEN ANY REQUESTS*
			;*   THE FOLLOWING SECTIONS UNTIL THE NEXT 	*
			;* COMMENT BLOCK DEAL WITH A AIR PRESSURE DELAY *
			;* ROUTINE.  THIS WILL ALLOW THE AIR PRESSURE	*
			;* TO REACH THE AIR PRESSURE SWITCH BEFORE WE	*
			;* CALL OUT TO CHECK ON IT.			*
			;*   THIS PREVENTS A RACE BETWEEN THE AIR 	*
			;* PRESSURE AND THE PROCESSOR.  IF THIS ROUTINE	*
			;* WERE LEFT OUT THE MCR WOULD TURN ON THE MAIN	*
			;* AIR SOLENOID AND THEN CHECK IF THERE WAS AIR	*
			;* PRESSURE.  IF NOT THEN THE MCR (AND MAIN AIR *
			;* SOLENOID) WOULD BE DISABLED.			*
			;************************************************


UPDATE:	PUSH	AF		;SAVE ALL THE REGISTERS
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	DISPOLL
	CALL	CHREC		;CALL THE TT5 
	LD	A,(REG2)
	BIT	3,A		;IS THE MCR BEING DISABLED
	JP	NZ,KILLMCR
	IN	A,05H
	BIT	7,A		;IS THE MCR ENABLED
	JP	Z,MCROUT
	LD	A,(TIMEREG)
	BIT	3,A		;THE AIR FINISHED FLAG
	JP	NZ,AIRDONE
	CALL	NOAIR		;CALL TO WAIT FOR AIR UP
	JP	UPEND
AIRDONE:IN	A,00H
	BIT	4,A		;IS AIR PRESSURE UP
	JP	NZ,MCR
	CALL	AIROFF
	JP	UPEND

MCROUT:	LD	A,(CALREG)
	BIT	0,A
	JP	Z,NOTCAL
	CALL	RESCAL		;SHUT DOWN THE CALIBRATE PROCESS IF E STOPPED
NOTCAL:	LD	A,(CALREG)
	BIT	7,A
	JP	Z,CALNOT
	LD	A,(CALREG)
	RES	7,A
	LD	(CALREG),A
	LD	A,(ESIRLY)
	CP	00H
	JP	Z,CALNOT
	NOP			;FUTURE LOCATION TO DISPLAY ERROR MESSAGE
	NOP			;IF TT5 IS CONNECTED OR NOT
	NOP
	LD	A,00H
	LD	(ESIRLY),A	;RESET THE ERROR REGISTER
	OUT	01H,A		;TURN OFF THE ERROR LIGHT
CALNOT:	LD	A,(TIMEREG)
	BIT	3,A		;IS THE AIR FINISHED FLAG SET
	JP	Z,MCROFF	;IF NOT GO ON TO CHECK THE RUNNING FLAG
	LD	A,(TIMEREG)
	RES	3,A		;RESET THE AIR FINISHED FLAG
	LD	(TIMEREG),A
	JP	MCROFF
	

NOAIR:	LD	A,(TIMEREG)
	BIT	2,A		;ARE WE IN THE AIR ON DELAY LOOP
	JP	NZ,DECAIR
	LD	HL,(AIRCNT)
	LD	(AIRDWN),HL	;LOAD THE AIR WAIT COUNT
	LD	A,(TIMEREG)
	SET	2,A		;SET THE AIR ON DELAY LOOP
	LD	(TIMEREG),A
	RET

DECAIR:	LD	HL,(AIRDWN)
	DEC	HL
	LD	(AIRDWN),HL
	LD	A,H
	CP	00H
	RET	NZ
	LD	A,L
	CP	00H
	RET	NZ
	IN	A,00H
	BIT	4,A		;IS THE AIR ON NOW AFTER DELAY
	JP	NZ,AIRON
	LD	A,(TIMEREG)
	RES	2,A		;RESET THE AIR DELAY FLAG
	LD	(TIMEREG),A
	CALL	AIROFF
	RET

AIRON:	LD	A,(TIMEREG)
	SET	3,A		;SET THE AIR FINISHED FLAG
	RES	2,A		;RESET THE AIR DELAY FLAG
	LD	(TIMEREG),A
	RET

AIROFF:	LD	A,(REG2)
	SET	3,A		;SET THE MCR DISABLING FLAG
	LD	(REG2),A
	LD	A,(PORT06)
	RES	7,A		;RESET THE MCR OPTO-22 MODULE
	OUT	06H,A
	LD	(PORT06),A
	LD	HL,(MCRCNT)
	LD	(MCRDWN),HL
	RET

KILLMCR:LD	HL,(MCRDWN)
	DEC	HL		;DECREMENT THE MCR DISABLE TIME COUNT
	LD	(MCRDWN),HL
	LD	A,H
	CP	00H
	JP	NZ,UPEND
	LD	A,L
	CP	00H
	JP	NZ,UPEND
	LD	A,(PORT06)
	SET	7,A		;TURN ON THE OPTO-22 MODULE AFTER MCR IS OUT
	OUT	06H,A
	LD	(PORT06),A
	LD	A,(REG2)
	RES	3,A		;RESET THE DISABLING FLAG FOR THE MCR
	LD	(REG2),A
	JP	MCROUT
	


			;************************************************
			;*	TO GET TO THIS ROUTINE THE MCR IS      	*
			;*  ENABLED AND THE AIR PRESSURE IS PRESENT.    *
			;*  THIS IS THE NORMAL PATH FOR THE PROGRAM TO	*
			;*  FOLLOW IN THE OPERATION OR RUNMODE.		*
			;*      CHECKS ARE MADE OF THE SETUP SWITCH,	*
			;*  THE LOAD DISABLE SWITCH, THE SYNTRON BOWL	*
			;*  TIMER, THE AUTO OVEN OFF ROUTINE, THE	*
			;*  KICKOUT TIMER AND PART TEST TIMER, AND	*
			;*  IF A PART HAS PASSED BY THE PART SENSOR.	*
			;*  THE END OF ORDER BEEPER IS CHECKED TO SEE	*
			;*  IF THE BEEPER IS ACTIVE OR NEEDS TO BE 	*
			;*  TURNED OF IF TIMED OUT			*
			;*      IF THE MACHINE IS RUNNING THE PART 	*
			;*  LEVEL IN THE RIFFLE RACK IS CHECKED AND THE	*
			;*  THE LOW PART HISTORY FLAG MODIFIED.		*
			;************************************************

MCR:	CALL	SETUP		;CALL TO CHECK ON SETUP MODE.
	CALL	LOADER		;CALL TO CHECK ON LOAD DISABLER
	CALL	TIME1		;CALL TO CHECK ON PART LEVEL
	CALL	OVENT		;CHECK ON THE OVEN AUTO OFF AND STOP REQ.
	CALL	TIMER		;CHECK THE KICKOUT AND PART TEST TIMERS.
	CALL	PARCHK		;CHECK IF A PART HAS PASSED THE SENSOR
	CALL	SPDCHK		;CHECK ON THE SYNTRON BOWL
	CALL	CHUTEO		;CHECK ON THE LOAD CHUTE POSITION
	CALL	SYNHIS		;CHECK IF THE PARTS ARE LOW IN THE RIFFLE RACK
	CALL	BEEP		;CHECK IF THE END OF ORDER BEEPER IS ACTIVE
	LD	A,(REG1)	;CHECK IF TESTMARK IS IN RUNNING MODE.
	BIT	1,A
	JP 	Z,MCRCAL	;IF NOT RUNNING CHECK IF CALIBRATING.
	JP	RUNMODE



			;************************************************
			;*	TO GET TO THIS ROUTINE THE MASTER 	*
			;*  CONTROL RELAY IS NOT ENABLED.		*
			;*	THE ROUTINE WILL CHECK THE STATE OF THE *
			;*  RUNNING FLAG.  IF THE MACHING IS NOT RUNNING*
			;*  THE PROGRAM WILL JUMP TO RESTORE THE 	*
			;*  REGISTERS AND RETURN TO THE MAIN PROGRAM.	*
			;*  IF THE MACHINE IS RUNNING AT THIS TIME THE	*
			;*  FLAG WILL BE RESET AND THE OUTPUTS ALL 	*
			;*  RESET.  A FLAG WILL BE SET THAT WILL 	*
			;*  INDICATE A MCR DISABLE OCCURED BECAUSE OF 	*
			;*  LOSS OF AIR PRESSURE DURING A RUN CYCLE.	*
			;************************************************

MCROFF:	LD 	A,(REG1)	;CHECK IF IN RUNNING MODE
	BIT	1,A
	JP	Z,UPEND
	RES	1,A		;RESET THE RUNNING MODE FLAG.
	LD	(REG1),A	
	LD	A,(PORT06)
	RES	6,A		;RESET THE MOTOR
	RES	5,A		;RESET THE OVEN
	RES	4,A		;RESET THE SYNTRON BOWL
	RES	3,A		;RESET THE KICKOUT BIT
	RES	2,A		;RESET THE RACK VIBRATOR
	OUT	06H,A	
	LD	(PORT06),A
	LD	A,(REG2)	;IN	A,01H
	BIT	4,A		;IS THE LOAD DISABLE FLAG ACTIVE. BIT 2,A
	JP	NZ,OFFMCR
	LD	A,(PORT06)
	RES	1,A		;RESET THE LOAD DISABLER IF SWITCH NOT ACTIVE
	OUT	06H,A
	LD	(PORT06),A
OFFMCR:	LD 	A,(REG1)	;SET A BIT THAT SAYS A DISABLE OCCURED.
	SET	0,A
	LD	(REG1),A
	JP 	UPEND

			;************************************************
			;*	TO GET TO THIS ROUTINE,  THE MACHINE	*
			;*  AIR PRESSURE MUST BE UP,  THE MASTER CONTROL*
			;*  RELAY MUST BE ENABLED, AND THE MACHINE 	*
			;*  RUNNING FLAG MUST NOT BE SET.		*
			;*	THIS SECTION WILL CHECK THE STATE OF THE*
			;*  CALIBRATION STOP FLAG AND IF SET THE PROGRAM*
			;*  WILL JUMP TO RESORE THE REGISTERS AND RETI.	*
			;*	IF THE CALIBRATION STOP IS NOT SET, THE	*
			;*  KICKOUT FLAG WILL BE CHECKED IN CASE THE 	*
			;*  MACHINE WAS STOPED IN THE MIDDLE OF AN EJECT*
			;*  SEQUENCE.  IF THE KICKOUT FLAG IS SET IT 	*
			;*  DECREMENTS THE COUNT AND IF ZERO RESETS ALL	*
			;*  OF THE REGISTERS AND KICKOUT SOLENOID.	*
			;*  	AT THE END OF THIS ROUTINE THE START	*
			;*  JOG, AND CALIBRATE SWITCHES ARE CHECKED TO	*
			;*  SEE IF ANY PROCESS IS REQUESTED.  		*
			;*      TO BE ABLE TO CALIBRATE WE MUST NOT 	*
			;*  HAVE ALREADY CALIBRATED. ONCE THE MACHINE	*
			;*  IS CALIBRATED WE CAN THEN ENTER THE SETUP	*
			;*  MODE, REQUEST A START, OR REQUEST A JOG.	*
			;************************************************
	

MCRCAL:	LD	A,(CALREG)
	BIT	4,A
	JP	NZ,UPEND	;IF IN CALIBRATION MODE RETI.
	LD	A,(TIMEREG)
	BIT	0,A		;IS THE EJECT FLAG BIT SET
	JP	Z,NOTOUT
	LD	A,(OUTDWN)
	DEC	A		;DECREMENT THE 10mS TIMER COUNT.
	LD	(OUTDWN),A
	LD	A,(OUTDWN)
	CP	00H
	JP	Z,UPEND		;IF THE COUNT IS ZERO, RESET THE REGISTERS
	LD	A,(TIMEREG)
	RES	0,A		;RESET THE KICKOUT FLAG
	LD	(TIMEREG),A
	LD	A,(PORT06)
	RES	3,A		;RESET THE KICKOUT SOLENOID
	OUT	06H,A	
	LD	(PORT06),A
NOTOUT:	LD	A,(CALREG)
	BIT	2,A		;ARE WE CALIBRATING
	JP	NZ,UPEND
	IN	A,00H
	BIT	7,A		;CHECK IF PRINTER DOOR OPEN
	JP	Z,UPCHK		;IF SO DO NOT CHECK FOR START REQUESTS
	LD	A,(TIMEREG)
	BIT	7,A		;ARE WE IN SETUP MODE
	JP	Z,UPEND
UPCHK:	LD	A,(CALREG)
	BIT	7,A		;HAVE WE CALIBRATED
	JP	NZ,STRJOG	;IF YES THEN DO NOT CALIBRATE AGAIN
				;LD	A,(TIMEREG)
				;BIT	7,A		SETUP SWITCH ACTIVE
				;JP	NZ,UPEND
	IN	A,00H
	BIT	0,A		;HAS CALIBRATE BEEN REQUESTED
	JP	NZ,ARECAL
	JP	UPEND

STRJOG:	IN	A,00H
	BIT	1,A		;HAS JOG BEEN REQUESTED
	JP	NZ,AREJOG
	IN	A,00H
	BIT	2,A		;HAS START BEEN REQUESTED
	JP	NZ,ARESTR
	JP	UPEND


			;************************************************
			;*	THESE ROUTINES SET A REQUEST FLAG THAT	*
			;*  INDICATES A START, JOG, OR CALIBRATE BUTTON *
			;*  WAS PRESSED.  THESE FLAGS WILL BE VERIFIED  *
			;*  IN THE MAIN PROGRAM WHEN THE MODULE RETURNS	*
			;************************************************


ARECAL:	LD	A,(CALREG)
	SET	0,A		;SET CALIBRATE PROCESS FLAG
	SET	1,A		;SET THE CALIBRATE REQUEST FLAG
	LD	(CALREG),A
	JP	UPEND

AREJOG:	LD	A,(RUNREG)
	SET	1,A		;SET THE JOG REQUEST FLAG
	LD	(RUNREG),A
	JP	UPEND

ARESTR:	LD	A,(RUNREG)
	SET	0,A		;SET THE START REQUEST FLAG
	LD	(RUNREG),A
	JP 	UPEND


			;************************************************
			;*	THIS ROUTINE WILL BE JUMPED TO WHENEVER	*
			;*  THE PROGRAM IS READY TO RETURN TO THE MAIN	*
			;*  PROGRAM FROM THE INTERRUPT.  IT WILL RESTORE*
			;*  THE REGISTERS BEING USED IN THE MAIN PROGRAM*
			;*  AND THEN RETURN.				*
			;************************************************

UPEND:	POP	HL	;RESET ALL THE SAVED REGISTERS.
	POP	DE
	POP	BC
	POP	AF
	RETI

			;************************************************
			;*  	THIS ROUTINE IS CALLED TO CHECK THE 10mS*
			;*  EJECT AND TEST COUNTERS THAT WERE SET BY THE*
			;*  PULSE ROUTINE AND THE 10mS COUNTER FOR THE 	*
			;*  SYNTRON BOWL THAT WAS SET BY THE SYNCHK AND *
			;*  CHUTEO ROUTINES IN THIS TIMER MODULE.	*
			;*  	THE ROUTINES WILL DECREMENT THEIR	*
			;*  RESPECTIVE COUNT REGISTERS IF THE REQUEST	*
			;*  BITS ARE SET. WHEN THE COUNT REGISTERS REACH*
			;*  ZERO THE OUTPUTS ARE RESET AND REQUEST FLAGS*
			;*  ARE RESET.  IF THE COUNT IS NOT EQUAL TO 	*
			;*  ZERO AFTER DECREMENTING THE FLAG WILL NOT	*
			;*  BE RESET AND THE COUNT WILL BE CHECKED AGAIN*
			;*  AT THE NEXT 10mS PULSE.			*
			;************************************************

TIMER:	LD 	A,(TIMEREG)	;CHECK IF KICKOUT FLAG IS SET
	BIT	0,A
	JP	Z,TIME2
	LD	A,(OUTDWN)	;DECREMENT TIME BY 1 (10mSEC)
	DEC	A
	LD	(OUTDWN),A
	CP	00H		;CHECK IF COUNT HAS REACHED ZERO
	JP	NZ,TIME2
	LD	A,(TIMEREG)	;WHEN TIME IS OUT, RESET FLAG
	RES	0,A
	LD	(TIMEREG),A
	LD	A,(PORT06)	;WHEN TIME IS OUT,RESET THE KICKOUT SOLENOID.
	RES	3,A
	OUT	06H,A
	LD	(PORT06),A
	JP 	TIME2

TIME1:	LD	A,(TIMEREG)	;CHECK IF LOW PART FLAG IS SET
	BIT	1,A
	JP	Z,BYEBYE
	LD	HL,(BOWLDWN)	;IF SET, DECREMENT BY 1 (10mSEC)
	DEC	HL
	LD	(BOWLDWN),HL
	LD	A,H
	CP	00H		;CHECK IF COUNT HAS REACHED ZERO (TIMED OUT)
	JP	NZ,BYEBYE	
	LD	A,L
	CP	00H
	JP	NZ,BYEBYE
	LD	A,(TIMEREG)	;WHEN TIME IS OUT, RESET FLAG
	RES	1,A
	LD	(TIMEREG),A
	LD	A,(PORT06)	;WHEN TIME IS OUT,TURN OFF SYNTRON BOWL
	RES	4,A
	OUT	06H,A
	LD	(PORT06),A
BYEBYE:	RET

TIME2:	LD 	A,(RUNREG)	;CHECK IF PART TEST FLAG IS SET.
	BIT	2,A
	RET	Z
	LD	A,(ESIDWN)	;DECREMENT TIME BY 1 (10mSEC)
	DEC	A
	LD	(ESIDWN),A
	CP	00H		;CHECK IF COUNT HAS REACHED ZERO (TIMED OUT)
	RET	NZ
	LD	A,(PORT04)
	SET	7,A		;WHEN TIME IS OUT, SET THE N.O. CONTACT
	RES	6,A		;WHEN TIME IS OUT, RESET THE COMM2 CONTACT
	OUT	04H,A		;ON THE ESI BRIDGE TO ZERO
	LD	(PORT04),A
	LD	A,(RUNREG)	;
	RES	2,A		;WHEN TIME IS OUT, RESET THE TEST FLAG
	LD	(RUNREG),A	;
	LD	A,(RUNREG)	;WHEN TIME IS OUT, SET A FLAG TO INDICATE
	SET	3,A		;A COMPLETED TEST.
	LD	(RUNREG),A	;
	RET

			;************************************************
			;*	THIS ROUTINE WILL CHECK THE LEVEL OF THE*
			;*  RIFFLE RACK.  IF THE PARTS ARE LOW IT WILL	*
			;*  SET A FLAG THAT WILL BE DOUBLE CHECKED IN 	*
			;*  ANOTHER ROUTINE.				*
			;************************************************

SYNHIS:	IN	A,01H		;CHECK IF THE PARTS ARE LOW IN THE RACK.
	BIT	0,A
	RET	NZ
	LD	A,(REG2)	;SET THE PARTS LOW HISTORY BIT IF THE PARTS
	SET	2,A		;ARE LOW IN THE RIFFLE RACK. 
	LD	(REG2),A
	RET

			;************************************************
			;*	THIS ROUTINE DEALS WITH THE SYNTRON BOWL*
			;*  CONTROL.  IF THE BOWL IS ALREADY ON THE 	*
			;*  ROUTINE WILL GO ON TO THE NEXT ROUTINE.  IF	*
			;*  THE SYNTRON BOWL IS NOT ACTIVE THE LOW LEVEL*
			;*  HISTORY FLAG IS CHECKED AND IF NOT SET THE 	*
			;*  NEXT ROUTINE WILL BE JUMPED TO.  		*
			;*  	IF THE HISTORY FLAG IS SET THE LEVEL OF	*
			;*  THE RIFFLE RACK WILL BE CHECKED AGAIN.  IF	*
			;*  IT IS NO LONGER LOW,  THE HISTORY BIT WILL	*
			;*  BE RESET AND THE NEXT ROUTINE WILL BE JUMPED*
			;*  TO.  IF THE RACK IS STILL LOW, THE SYNTRON 	*
			;*  BOWL PROHIBIT IS CHECKED.  IF IT IS SET THE *
			;*  NEXT ROUTINE WILL BE JUMPED TO.  IF IT IS 	*
			;*  CLEAR THE BOWL 10mS TIMER REQUEST FLAG WILL	*
			;*  BE SET,  THE BOWL COUNT WILL BE LOADED, THE	*
			;*  HISTORY BIT RESET, AND THE BOWL TURNED ON.	*
			;*	THE PROGRAM WILL THEN JUMP TO THE NEXT 	*
			;*  ROUTINE.					*
			;************************************************

SPDCHK:	LD	A,(TIMEREG)	;IS SYNTRON BOWL CONTROL BIT SET.
	BIT	1,A
	JP	NZ,CHKSPD
	LD	A,(REG2)	;CHECK IF SYNTRON HISTORY BIT IS SET.
	BIT 	2,A
	JP	Z,CHKSPD
	IN	A,01H
	BIT	0,A
	JP	NZ,RESSYN
	LD	A,(REG2)
	BIT	1,A		;CHECK IF THE SYNTRON PROHIBIT BIT IS SET.
	JP	NZ,CHKSPD
	LD	A,(TIMEREG)
	SET	1,A		;SET THE LOWPARTS FLAG
	LD	(TIMEREG),A
	LD	A,(PORT06)
	SET	4,A		;TURN ON THE SYNTRON BOWL
	OUT	06H,A
	LD	(PORT06),A
	LD	HL,(BOWLCNT)
	LD	(BOWLDWN),HL	;LD THE BOWL 10mS COUNT FROM THE MEMORY
	LD	A,(REG2)	;RESET THE HISTORY BIT
	RES	2,A
	LD	(REG2),A
CHKSPD:	RET

RESSYN:	LD	A,(REG2)
	RES	2,A		;RESET THE HISTORY BIT FOR LOWPARTS TEST.
	LD	(REG2),A
	JP	CHKSPD

			;****************************************
			;*   THIS CALLED ROUTINE CONTINUALLY 	*
			;* MONITORS THE PART PRESENT SENSOR	*
			;* AND WILL SET A FLAG IF IT FOUND. THE	*
			;* PARTIT ROUTINE IN THE PULSE MODULE	*
			;* LOOKS AT THIS FLAG AND SETS THE SHIFT*
			;* REGISTER ACCORDINGLY.  IT ALSO RESETS*
			;* THIS FLAG.  THIS OCCURS ONCE PER PART*
			;****************************************

PARCHK:	LD	A,(RUNREG)
	BIT	6,A		;CHECK IF PART HAS ALREADY BEEN FOUND
	JP	NZ,CHKPAR
	IN	A,01H
	BIT	1,A		;CHECK IF THE PART IS HERE NOW
	JP	Z,CHKPAR
	LD	A,(RUNREG)
	SET	6,A		;SET FLAG TELLING PART WAS PRESENT
	LD	(RUNREG),A
CHKPAR:	RET


			;************************************************
			;*	THIS ROUTINE CHECKS THE LOAD DISABLE	*
			;*  SWITCH.  IF IT IS NOT ACTIVE THE ROUTINE 	*
			;*  WILL THEN CHECK THE LOWER RIFFLE RACK SENSOR*
			;*  TO SEE IF THE PARTS ARE LOW.  IF THE PARTS	*
			;*  ARE NOT LOW OR IF THE SENSOR HAS BEEN DIS-	*
			;*  ABLED BY THE FRONT PANEL SWITCH THE ROUTINE	*
			;*  THEN CHECKS TO SEE THAT THE FLAG IS NOT SET	*
			;*  AND IF IT IS IT WILL BE RESET AND THE LOAD	*
			;*  DISABLER TURNED OFF.			*
			;*	IF EITHER THE LOAD DISABLE SWITCH IS	*
			;*  ACTIVATED OR IF THE PARTS GET TO LOW IN THE	*
			;*  RIFFLE RACK THE FLAG WILL BE CHECKED TO SEE *
			;*  IF IT IS SET.  IF IT IS NOT SET IT WILL BE	*
			;*  SET AND THE LOAD DISABLER WILL BE TURNED ON.*
			;************************************************

LOADER:	IN	A,01H
	BIT	2,A		;IS THE LOAD DISABLE SWITCH ACTIVE
	JP	NZ,LOADIT
	BIT	3,A		;ARE THE PARTS LOW IN THE RACK OR HAS THE
	JP	Z,NOLOAD	;SENSOR BEEN DISABLED.
LOADIT:	LD	A,(REG2)	;SWITCH ACTIVE OR PARTS LOW (NOT DISABLED)
	BIT	4,A		;IS THE LOADER FLAG SET
	RET	NZ
	LD	A,(REG2)
	SET	4,A		;SET THE LOADER FLAG
	LD	(REG2),A
	LD	A,(PORT06)
	SET	1,A		;TURN ON THE LOAD DISABLER
	OUT	06H,A	
	LD	(PORT06),A
	RET
NOLOAD:	LD	A,(REG2)
	BIT	4,A		;IS THE LOADER FLAG SET
	RET	Z
	LD	A,(PORT06)
	RES	1,A		;TURN OFF THE LOAD DISABLER
	OUT	06H,A
	LD	(PORT06),A
	LD	A,(REG2)
	RES	4,A		;RESET THE LOADER FLAG
	LD	(REG2),A
	RET


			;************************************************
			;*	THIS ROUTINE IS USED TO CHECK THE STOP	*
			;*  REQUEST FLAGS AND THE LOAD DISABLE SWITCH.	*
			;*  IF VERIFIED STOP REQUEST IS SET THE PROGRAM	*
			;*  WILL JUMP TO RESTORE THE MAIN PROGRAM 	*
			;*  REGISTERS AND THEN RETURN.			*
			;*	IF THE PRINTER DOOR IS OPEN, THE END	*
			;*  OF ORDER SWITCH IS ACTIVE, OR THE STOP 	*
			;*  REQUEST SWITCH IS ACTIVE A HISTORY FLAG IS	*
			;*  SET THAT WILL BE VERIFIED IN THE MAIN 	*
			;*  PROGRAM. 					*
			;*	THE LOAD DISABLE SWITCH IS CHECKED TO	*
			;*  DETERMINE IF THE SYNTRON BOWL AND VIBRATOR	*
			;*  SHOULD BE ALLOWED TO BE ACTIVE OR NOT.	*
			;*	THE ROUTINE THEN JUMPS TO CHECK IF THE	*
			;*  LOAD CHUTE IS OUT OF POSITION OR NOT.	*
			;*	IF WE ARE IN THE SETUP MODE THE PRINTER	*
			;*  DOOR IS NOT LISTENED TO SO THE HEADS CAN 	*
			;*  BE CLEANED WITH THE MACHINE RUNNING.	*
			;************************************************

RUNMODE:LD	A,(REG1)
	BIT	6,A		;CHECK IF BEEP PROCESS FLAG IS SET
	JP	NZ,STPREQ

	IN	A,00H
	BIT	5,A		;IS THE END OF ORDER BEING REQUESTED
	JP	Z,STPREQ

	LD	A,(REG1)
	SET	5,A		;SET A DEBOUNCE END OF ORDER REQUEST FLAG
	LD	(REG1),A

STPREQ:	LD	A,(STPREG)
	BIT	7,A		;CHECK IF THE STOP REQUEST IS ALREADY SET
	JP	NZ,UPEND
	IN	A,00H
	BIT	3,A		;CHECK IF THERE IS A STOP REQUEST
	JP	NZ,ISASTP

	LD	A,(TIMEREG)
	BIT	7,A		;CHECK TO SEE IF WE ARE IN THE SETUP MODE.
	JP	NZ,NOMORE
	IN	A,00H
	BIT	7,A		;CHECK IF THE PRINTER DOOR IS OPEN
	JP	NZ,DOROPN
NOMORE:	JP	UPEND


			;************************************************
			;*	THESE ROUTINES SET HISTORY FLAGS TO	*
			;*  INDICATE THAT A STOP REQUEST NEEDS TO BE	*
			;*  SET.  THE ROUTINES ARE FOR A STOP REQUEST,	*
			;*  OR A PRINTER DOOR OPEN STOP.		*
			;************************************************

ISASTP:	LD	A,(STPREG)
	SET	0,A		;SET THE STOP REQUEST HISTORY BIT
	LD	(STPREG),A
	JP	NOMORE

	
DOROPN:	LD	A,(STPREG)
	SET	2,A		;SET THE PRINTER DOOR OPEN HISTORY BIT
	LD	(STPREG),A
	JP	NOMORE


			;************************************************
			;*	THIS ROUTINE IS CALLED TO CHECK THE   	*
			;*  LOAD CHUTE POSITION.  IF THE CHUTE IS OUT   *
			;*  OF POSITION THE SYNTRON BOWL WILL BE SHUT 	*
			;*  OFF.  WHEN THE CHUTE IS REPLAED BACK INTO	*
			;*  POSITION THE SYNTRON BOWL WILL BE ALLOWED 	*
			;*  TO RUN AGAIN.				*
			;************************************************

CHUTEO:	IN	A,00H
	BIT	6,A		;CHECK IF THE CHUTE IS OUT OF POSITION
	JP	NZ,CHUOUT
	LD	A,(REG2)
	BIT	5,A
	JP	NZ,OCHUTE
	LD	A,(PORT06)
	RES	4,A		;SYNTRON BOWL BIT
	RES	2,A		;VIBRATOR
	OUT	06H,A
	LD	(PORT06),A		
	LD	A,(REG2)
	SET	1,A		;SET THE SYNTRON PROHIBIT BIT
	SET	5,A		;SET THE CHUTE OUT FLAG
	LD	(REG2),A
	JP	OCHUTE
CHUOUT:	LD	A,(REG2)
	BIT	5,A
	JP	Z,OCHUTE
	LD	A,(REG2)
	RES	1,A
	RES	5,A
	LD	(REG2),A
	LD	A,(PORT06)
	SET	2,A		;VIBRATOR ON
	OUT	06H,A
	LD	(PORT06),A
OCHUTE:	RET




			;************************************************
			;*	THIS ROUTINE IS CALLED BY THE MCRCHK	*
			;*  ROUTINE TO RESET VARIOUS REGISTERS IF THE	*
			;*  MACHINE IS IN THE CALIBRATION PROCESS.	*
			;************************************************
RESCAL:	LD	A,(CALREG)
	BIT	0,A		;CHECK IF IN CALIBRATION PROCESS
	RET	Z
	LD	A,(CALREG)
	RES	0,A		;IF SO, RESET CALIBRATION PROCESS MODE FLAG
	RES	1,A		;RESET THE CALIBRATION REQUEST FLAG
	RES	2,A		;RESET THE   ARE CALIBRATING   FLAG
	RES	4,A		;RESET THE CALIBRATION STOP REQUEST FLAG
	LD	(CALREG),A
	LD	A,(STPREG)
	XOR	A		;CLEAR THE STOP REQUEST, FLAG REGISTER
	LD	(STPREG),A
	LD	A,(RUNREG)
	RES	0,A		;RESET THE START REQUEST FLAG
	RES	1,A		;RESET THE JOG REQUEST FLAG
	LD	(RUNREG),A
	LD	A,(PORT06)
	RES	6,A		;TURN OFF THE MOTOR
	OUT	06H,A
	LD	(PORT06),A
	LD	A,(REG2)	;IN	A,01H
	BIT	4,A		;IS THE LOAD DISABLE FLAG ACTIVE  BIT 2,A
	RET	NZ		;IF SO RETURN
	LD	A,(PORT06)
	RES	1,A		;RESET THE LOAD DISABLE
	OUT	06H,A
	LD	(PORT06),A
	RET


SETUP:	LD	A,(TIMEREG)
	BIT	7,A		;ARE WE IN THE SETUP MODE
	JP	Z,CHKUP
	IN	A,01H
	BIT	6,A		;IS THE SETUP MODE STILL REQUESTED.
	JP	NZ,SETRET	;IF IT IS NOTHING HAS CHANGED SO RETURN.
	LD	A,(TIMEREG)
	BIT	6,A		;ARE WE CLEARING THE SETUP FLAG.
	JP	NZ,SETRET	;IF WE ARE THEN RETURN
	LD	A,(REG1)
	BIT	1,A		;IS THE MACHINE RUNNING
	JP	Z,UNUP		;IF NOT RESET THE SETUP MODE FLAG.
	LD	A,(TIMEREG)
	SET	6,A		;SET THE CLEARING FLAG
	LD	(TIMEREG),A
	LD	A,(STPREG)
	BIT	7,A		;IS THE STOP REQUEST ALREADY SET
	JP	NZ,SETRET	;IF IT IS THEN RETURN
	SET	7,A		;OTHERWISE SET THE STOP REQUEST FLAG
	LD	(STPREG),A
	JP	SETRET		;RETURN

UNUP:	LD	A,(TIMEREG)
	RES	7,A		;RESET THE SETUP MODE FLAG
	LD	(TIMEREG),A
	JP	SETRET		;RETURN

CHKUP:	IN	A,01H
	BIT	6,A		;IS THE SET UP MODE REQUESTED
	JP	Z,SETRET
	LD	A,(CALREG)
	BIT	7,A		;HAVE WE CALIBRATED THE MACHINE
	JP	Z,SETRET	;DO NOT ALLOW SETUP MODE IF NOT CALIBRATED
	LD	A,(REG1)
	BIT	1,A		;IS THE MACHINE RUNNING
	JP	Z,ITUP		;IF NOT SET THE SETUP MODE FLAG
	LD	A,(STPREG)
	BIT	7,A		;IS THE STOP REQUEST FLAG ALREADY SET
	JP	NZ,SETRET
	SET	7,A		;SET THE STOP REQUEST IF IT IS NOT SET
	LD	(STPREG),A
	JP	SETRET

ITUP:	LD	A,(TIMEREG)
	SET	7,A		;SET THE SETUP MODE FLAG
	LD	(TIMEREG),A
SETRET:	RET

	
			;************************************************
			;*     THIS ROUTINE WILL CHECK IF THE OVEN IS ON*
			;* AND IF SO WORK WITH THE PULSE ROUTINE TO 	*
			;* SHUT THE OVEN OFF IT THE CHAIN SLOWS DOWN TOO*
			;* MUCH TO PREVENT WARPING THE CHAIN.		*
			;*     THE PULSE ROUTINE LOADS A COUNT INTO THE	*
			;* SLOWDWN REGISTER EVERY TIME A PULSE IS 	*
			;* RECEIVED.  THIS ROUTINE DECREMENTS THAT COUNT*
			;* EVERY 10mS IF THE OVEN IS ON.  IF THIS COUNT	*
			;* REACHES ZERO BEFORE THE PULSE ROUTINE RELOADS*
			;* THE FULL COUNT THE OVEN WILL BE SHUT OFF AND	*
			;* A STOP REQUEST GENERATED.  THE STOP REQUEST	*
			;* IS SET SO WHEN THE SPEED IS TURNED BACK UP	*
			;* AND THE MACHINE STOPS, THE START BUTTON WILL	*
			;* HAVE TO BE PRESSED WHICH WILL TURN THE OVEN 	*
			;* BACK ON AGAIN.  THIS WILL MAKE SURE THE OVEN	*
			;* IS TURNED ON TO DRY THE PARTS AFTER PRINTING.*
			;************************************************


OVENT:	LD	A,(PORT06)
	BIT	5,A		;IS THE OVEN ON
	RET	Z
	LD	HL,(OVENDWN)
	DEC	HL		;DECREMENT THE SLOW COUNT
	LD	(OVENDWN),HL
	LD	A,H
	CP	00H
	RET	NZ
	LD	A,L		;HAS THE COUNT TIMED OUT BEFORE THE
	CP	00H		;PULSE ROUTINE CAN RESET IT.
	RET	NZ
	LD	HL,(SLOWCNT)
	LD	DE,(SLOWDWN)	;CHECK IF THE PULSE COUNT HAS REACHED THE
	LD	A,H		;MINIMUM REQUIRED.
	SUB	D
	JP	Z,OKSF		;CHECK IF THE UPPER BYTE IS EQUAL
	JP	M,ALLOK		;CHECK IF THE UPPER BYTE IS GREATER
	JP	NOTOK
OKSF:	LD	HL,(SLOWCNT)
	LD	DE,(SLOWDWN)
	LD	A,L
	SUB	E
	JP	Z,ALLOK
	JP	M,ALLOK
NOTOK:	LD	A,(PORT06)
	RES	5,A
	OUT	06H,A
	LD	(PORT06),A
	LD	A,(STPREG)
	SET	7,A
	LD	(STPREG),A
	RET

ALLOK:	LD	HL,(OVENTME)
	LD	(OVENDWN),HL
	LD	HL,0000H
	LD	(SLOWDWN),HL
	RET


			;****************************************
			;*     THIS ROUTINE DEALS WITH THE END	*
			;*  OF ORDER SENSOR.  WHEN THE SWITCH 	*
			;*  WAS ASSERETED BY THE ORDER SEPERATOR*
			;*  A BEEPER WAS TURNED ON.  THIS CALLED*
			;*  ROUTINE WILL DECREMENT EVERY 10mS	*
			;*  AND TURN OFF THE BEEPER WHEN THE 	*
			;*  COUNT HAS REACHED ZERO.  ONCE THE 	*
			;*  BEEPER HAS SHUT OFF THE END OF ORDER*
			;*  SWITCH IS CHECKED AGAIN.  IF IT IS	*
			;*  STILL ACTIVE A FLAG (BEEPED) WILL BE*
			;*  SET TO PREVENT THE BEEPER FROM BEING*
			;*  STARTED AGAIN.  THIS IS DONE IN CASE*
			;*  THE MACHINE IS STOPPED WITH THE 	*
			;*  BREAKER UNDER THE SWITCH CAUSING THE*
			;*  THE TONE TO CONTINUOUSLY BEING 	*
			;*  SOUNDED.				*
			;*    THE TONE WILL ONLY BE HEARD ONCE 	*
			;*  FOR ITS DESIGNATED AMOUNT OF TIME 	*
			;*  IF EITHER THE SWITCH IS MOMENTARILY	*
			;*  ACTIVE OR IF IT HELD LONGER.	*
			;****************************************


BEEP:	LD	A,(REG1)
	BIT	6,A		;IS THE BEEP PROCESS FLAG SET
	RET	Z

	LD	A,(REG1)
	BIT	7,A		;IS THE BEEPED FLAG SET
	JP	NZ,EOOCHK

	LD	A,(BEEPCNT)
	DEC	A		;DECREMENT THE BEEPER ON COUNT
	LD	(BEEPCNT),A
	CP	00H		;SEE IF THE BEEPER HAS TIMED OUT
	RET	NZ

	LD	A,(REG1)	;IF SO SET THE BEEPED FLAG
	SET	7,A
	LD	(REG1),A

	IN	A,01H
	BIT	7,A		;IS THE ERROR LIGHT ON
	JP	Z,BEEPOFF
	LD	A,0A0H		;TURN OFF THE BEEPER (0 OUTPUT AT 7507 CARD)
	OUT	01H,A		;& TURN ON THE ERROR LIGHT BUT NO INPUT PORTS
	RET			;****ADDED AS A DEBUG STEP

	
BEEPOFF:LD	A,20H		;LOAD A COUNT TO TURN OFF THE BEEPER BUT NOT
	OUT	01H,A		;TURN ON THE ERROR LIGHT SINCE IT WAS NOT ON.
	RET			;******ADDED AS A DEBUG STEP

EOOCHK:	LD	A,00H		;CHECK THE END OF ORDER SWITCH
	BIT	5,A
	RET	NZ		;IF IT IS STILL SET RETURN
	LD	A,(REG1)
	RES	6,A		;WHEN IT IS NO LONGER RIDING ON THE BREAKER
	RES	7,A		;RESET THE BEEPED FLAG AND THE BEEP REQUEST.
	LD	(REG1),A
	RET


	END